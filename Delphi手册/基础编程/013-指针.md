
指针，是C语言中的一个重要概念及其特点，也是掌握C语言比较困难的部分。**指针也就是内存地址**，**指针变量是用来存放内存地址的变量**，不同类型的指针变量所占用的存储单元长度是相同的，而存放数据的变量因数据的类型不同，所占用的存储空间长度也不同。有了指针以后，不仅可以对数据本身，也可以对存储数据的变量地址进行操作

> 引自百度百科

这里面提到了两个比较重要的名词：**指针**和**指针变量**，其实只要理清楚这两个东西就会感觉指针并不难（基础使用）

C语言之所以强大，以及其自由性，很大部分体现在其灵活的指针运用上。因此，说指针是C语言的灵魂，一点都不为过。

这种说法也让很多人产生误解，似乎只有C语言的指针才能算指针。其他语言不支持指针，其实，Pascal语言本身也是支持指针的。从最初的Pascal发展至今的Object Pascal，可以说在指针运用上，丝毫不会逊色于C语言的指针。

## 类型指针

类型指针的定义。对于指向特定类型的指针，在C中是这样定义的：

```c
int *ptr;
char *pcr;
```

与之等价的Object Pascal是如何定义的呢？

```c
var
ptr : ^Integer;
pcr : ^char;
```

> 针对ptr和pcr这种变量我们就可以称为指针变量，和普通变量相比而言他们的值不一样而已，类似于下图这样

![](_v_images/20210211152534247_17820.png)


## 无类型指针

所谓无类型指针类似于老外心目中的上帝

C中有void *类型，也就是可以指向任何类型数据的指针。Object Pascal为其定义了一个专门的类型：Pointer。于是，`ptr : Pointer; `就与C中的`void *ptr;`等价了。

> 在新版本的Delphi中其实很少使用指针了，所以个人建议无类型指针更应该少用甚至不用

## 基本使用

主要是指针的解除引用和取地址，不说了直接代码演示


```c
var
  // 定义普通变量，因为是全局变量可以直接赋初始值
  Age: Integer = 10;

  // 定义同类型的指针变量,可以使用^数据类型的形式，他们是等同的
  PAge: PInteger;

begin
  // 给指针变量赋值,它只能接收这样的值
  PAge := @Age;

  // 获取指针变量PAge的值，即变量Age的内存地址
  Writeln(IntToHex(Integer(PAge)));

  // 直接获取变量Age的内存地址
  Writeln(IntToHex(Integer(@Age)));

  // 获取指针变量中存储的具体值(解除指针引用)
  Writeln(PAge^);

  readln;

end.
```

> 上面代码演示的仅仅为一级指针，指针是可以嵌套的




##  指针运算

在C中，可以对指针进行移动的运算，如：

```C
char a[20];
char *ptr=a;
ptr++;
ptr+=2;

```
当执行ptr++;时，编译器会产生让ptr前进sizeof(char)步长的代码，之后，ptr将指向a[1]。ptr+=2;这句使得ptr前进两 个sizeof(char)大小的步长。同样，我们来看一下Object Pascal中如何实现

```c
var
   a : array [1..20] of Char;
   ptr : PChar; //PChar 可以看作 ^Char
begin
   ptr := @a;
   Inc(ptr); // 这句等价于 C 的 ptr++;
   Inc(ptr, 2); //这句等价于 C 的 ptr+=2;
end;
```

> 可能我接触Delphi 的时间比较短，指针运算这一块我几乎没怎么用过，作为一个知识点整理出来

### 字符数组的运算

C语言中，是没有字符串类型的，因此，字符串都是用字符数组来实现，于是也有一套str打头的库函数以进行字符数组的运算,

```C
char str[15];
char *pstr;
strcpy(str, "teststr");
strcat(str, "_testok");
pstr = (char*) malloc(sizeof(char) * 15);
strcpy(pstr, str);
printf(pstr);
free(pstr);
```

而在Object Pascal中，有了String类型，因此可以很方便的对字符串进行各种运算。但是，有时我们的Pascal代码需要与C的代码交互（比如：用 Object Pascal的代码调用C写的DLL或者用Object Pascal写的DLL准备允许用C写客户端的代码）的话，就不能使用String类型了，而必须使用两种语言通用的字符数组。其实，Object Pascal提供了完全相似C的一整套字符数组的运算函数，以上那段代码的Object Pascal版本是这样的：

```c
var str : array [1..15] of char;
   pstr : PChar; //Pchar 也就是 ^Char
begin
   StrCopy(@str, 'teststr'); //在C中，数组的名称可以直接作为数组首地址指针来用
    //但Pascal不是这样的，因此 str前要加上取地址的运算符
   StrCat(@str, '_testok');
   GetMem(pstr, sizeof(char) * 15);
   StrCopy(pstr, @str);
   Write(pstr);
   FreeMem(pstr);
end;
```

## 函数指针

函数指针能用于两种不同的目的：声明函数指针类型的变量；或者把函数指针作为参数传递给另一例程。

Delphi中可以通过函数指针把一个函数作为参数来传递，然后在另外一个函数中调用。首先，申明函数指针类型TFunctionParameter

```c
type
          TFunctionParameter = function(const value : integer) : string; //函数指针

```


定义准备被作为参数传递的函数

```c
function One(const value : integer) : string;                            //函数-实例1
begin
  result := IntToStr(value) ;
end;

function Two(const value : integer) : string;                            //函数-实例2
begin
  result := IntToStr(2 * value) ;
end;
```


定义将要使用动态函数指针参数的函数

```c
function DynamicFunction(f : TFunctionParameter; const value : integer) : string;
begin
  result := f(value) ;
end;
```

上面这个动态函数的使用实例

```c
var
  s : string;
begin
  s := DynamicFunction(One,2006) ;
  ShowMessage(s) ; //will display "2006"

  s := DynamicFunction(Two,2006) ;
  ShowMessage(s) ; // will display "4012"
end;
```


虽然这种调用方法比直接调用麻烦了,那么我们为什么要用这种方式呢？

1. 因为在某些情况下，调用什么样的函数需要在实际中(运行时)决定,你可以根据条件来判断,实现用同一个表达,调用不同的函数,很是灵活.

2. 利用函数指针我们可以实现委托,委托在.NEt中被发挥的淋漓尽致,但Delphi同样能实现

3. 实现回调机制

例子

```c
//********************************************************
// 函数指针(指向一般函数和过程)
//*********************************************************
unit DelegateUnit;
interface
     procedure Func1;
     //定义两个函数型构相同但功能不同的函数
    function    FuncAdd(VarA , VarB : Integer):Integer;
    function    FuncSub(VarA , VarB : Integer):Integer;
type
    DelegateFunc1 = procedure;
    DelegateFuncCalc = function(VarA , VarB : Integer):Integer;
var
  I : Integer;
implementation

procedure Func1;
begin
      Writeln('Func1 was called!');
end;

function    FuncAdd(VarA , VarB : Integer):Integer;
begin
     Result := VarA + VarB;
end;
function    FuncSub(VarA , VarB : Integer):Integer;
begin
   Result := VarA - VarB;
end;
end.

```
调用

```c
program Delegate;
{$APPTYPE CONSOLE}
uses
  DelegateUnit;
 var
    ADelegateFunc1 : DelegateFunc1;
    ADelegateFuncCalc : DelegateFuncCalc;
 begin
     //通过函数指针调用过程
     ADelegateFunc1  := Func1;
     ADelegateFunc1 ;
    //通过同种方式调用不同函数
    ADelegateFuncCalc  := FuncAdd;
    Writeln(ADelegateFuncCalc(3,5));
    ADelegateFuncCalc  := FuncSub;
    Writeln(ADelegateFuncCalc(3,5));
end.

```


